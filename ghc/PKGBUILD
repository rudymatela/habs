# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

pkgname=ghc
pkgver=7.8.3
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
depends=('perl' 'gmp>=5.0' 'gcc')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl' 'happy' 'alex')
checkdepends=('python2')
install='ghc.install'
options=('strip' 'staticlibs')

provides=('haskell-array=0.5.0.0'
          'haskell-base=4.7.0.1'
          'haskell-binary=0.7.1.0'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-bytestring=0.10.4.0'
          'haskell-containers=0.5.5.1'
          'haskell-deepseq=1.3.0.2'
          'haskell-directory=1.2.1.0'
          'haskell-filepath=1.3.0.2'
          'haskell-ghc-prim=0.3.1.0'
          'haskell-haskeline=0.7.1.2'
          'haskell-haskell2010=1.1.2.0'
          'haskell-haskell98=2.0.0.3'
          'haskell-hoopl=3.10.0.1'
          'haskell-hpc=0.6.0.1'
          'haskell-integer-gmp=0.5.1.0'
          'haskell-old-locale=1.0.0.6'
          'haskell-old-time=1.1.0.2'
          'haskell-pretty=1.1.1.1'
          'haskell-process=1.2.0.0'
          'haskell-rts=1.0'
          'haskell-template-haskell=2.9.0.0'
          'haskell-terminfo=0.4.0.0'
          'haskell-time=1.4.2'
          'haskell-transformers=0.3.0.0'
          'haskell-unix=2.7.0.1'
          'haskell-xhtml=3000.2.1'
          'haskell-cabal=1.18.1.3')
replaces=('haskell-array<0.5.0.0'
          'haskell-base<4.7.0.1'
          'haskell-binary<0.7.1.0'
          'haskell-bin-package-db<0.0.0.0'
          'haskell-bytestring<0.10.4.0'
          'haskell-containers<0.5.5.1'
          'haskell-deepseq<1.3.0.2'
          'haskell-directory<1.2.1.0'
          'haskell-filepath<1.3.0.2'
          'haskell-ghc-prim<0.3.1.0'
          'haskell-haskeline<0.7.1.2'
          'haskell-haskell2010<1.1.2.0'
          'haskell-haskell98<2.0.0.3'
          'haskell-hoopl<3.10.0.1'
          'haskell-hpc<0.6.0.1'
          'haskell-integer-gmp<0.5.1.0'
          'haskell-old-locale<1.0.0.6'
          'haskell-old-time<1.1.0.2'
          'haskell-pretty<1.1.1.1'
          'haskell-process<1.2.0.0'
          'haskell-rts<1.0'
          'haskell-template-haskell<2.9.0.0'
          'haskell-terminfo<0.4.0.0'
          'haskell-time<1.4.2'
          'haskell-transformers<0.3.0.0'
          'haskell-unix<2.7.0.1'
          'haskell-xhtml<3000.2.1'
          'haskell-cabal<1.18.1.3')

source=("http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-src.tar.xz"
        "http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-testsuite.tar.xz"
        'build.mk' 'arch-gen-contents-index')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    cp "${srcdir}/build.mk" mk/build.mk
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr
    make -j 5
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # python2 rename
    sed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

    #make test
    make THREADS=9 test

    # zero unexpected failures on all tier 1 platforms - http://hackage.haskell.org/trac/ghc/ticket/5757
    # enable this when upstream has 0 test failures from a simple 'make test'
    #make fulltest
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR=${pkgdir} install

    # clean up the doc index, it's re-generated in ghc.install
    ( cd ${pkgdir}/usr/share/doc/ghc/html/libraries ;
    rm doc-index*.html frames.html index-frames.html index.html \
        ocean.css haddock-util.js \
        hslogo-16.png  minus.gif plus.gif synopsis.png
    )
    # install our own contents generator
    install -m755 ${srcdir}/arch-gen-contents-index \
        ${pkgdir}/usr/share/doc/ghc/html/libraries

    install -d ${pkgdir}/usr/share/licenses/ghc
    install -m644 LICENSE \
        ${pkgdir}/usr/share/licenses/ghc
}

sha256sums=('b0cd96a549ba3b5e512847a4a8cd1a3174e4b2b75dadfc41c568fb812887b958'
            '91ef5bd19d0bc1cd496de08218f7ac8a73c69de64d903e314c6beac51ad06254'
            'f6f08c0fd1098396beba515db06d6c5f3aac633b56e2d35cbcefaf76272ee9af'
            'f800cd0de93ae9926bb65c6324d6ce3e1ee814dae9c70c7a7bce28e5deb8245c')
